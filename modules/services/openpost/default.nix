# OpenPost - Multi-platform social media posting
# https://github.com/rodrgds/openpost
{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.vps.openpost;
  isCloud = cfg.edition == "cloud";

  # Host port (external) - must be unique per service
  openpostHostPort = 8090;
  # Container port (internal) - OpenPost listens on 8080 inside container
  openpostContainerPort = 8080;
  openpostPostgresUser = "openpost";
  openpostPostgresDatabase = "openpost";

  openpostFileSecrets = [
    {
      name = "jwt-secret";
      env = "OPENPOST_JWT_SECRET_FILE";
      target = "/run/secrets/openpost_jwt_secret";
      value = config.sops.placeholder.openpost_jwt_secret;
    }
    {
      name = "encryption-key";
      env = "OPENPOST_ENCRYPTION_KEY_FILE";
      target = "/run/secrets/openpost_encryption_key";
      value = config.sops.placeholder.openpost_encryption_key;
    }
    {
      name = "provider-apps";
      env = "OPENPOST_PROVIDER_APPS_FILE";
      target = "/run/secrets/openpost_provider_apps";
      value = config.sops.placeholder.openpost_provider_apps;
    }
    {
      name = "feedback-webhook";
      env = "OPENPOST_FEEDBACK_DESTINATION_URL_FILE";
      target = "/run/secrets/openpost_feedback_webhook";
      value = config.sops.placeholder.openpost_feedback_webhook;
    }
    {
      name = "x-client-id";
      env = "X_CLIENT_ID_FILE";
      target = "/run/secrets/openpost_twitter_client_id";
      value = config.sops.placeholder.openpost_twitter_client_id;
    }
    {
      name = "x-client-secret";
      env = "X_CLIENT_SECRET_FILE";
      target = "/run/secrets/openpost_twitter_client_secret";
      value = config.sops.placeholder.openpost_twitter_client_secret;
    }
    {
      name = "linkedin-client-id";
      env = "LINKEDIN_CLIENT_ID_FILE";
      target = "/run/secrets/openpost_linkedin_client_id";
      value = config.sops.placeholder.openpost_linkedin_client_id;
    }
    {
      name = "linkedin-client-secret";
      env = "LINKEDIN_CLIENT_SECRET_FILE";
      target = "/run/secrets/openpost_linkedin_client_secret";
      value = config.sops.placeholder.openpost_linkedin_client_secret;
    }
    {
      name = "threads-client-id";
      env = "THREADS_CLIENT_ID_FILE";
      target = "/run/secrets/openpost_threads_client_id";
      value = config.sops.placeholder.openpost_threads_client_id;
    }
    {
      name = "threads-client-secret";
      env = "THREADS_CLIENT_SECRET_FILE";
      target = "/run/secrets/openpost_threads_client_secret";
      value = config.sops.placeholder.openpost_threads_client_secret;
    }
    # {
    #   name = "mastodon-servers";
    #   env = "MASTODON_SERVERS_FILE";
    #   target = "/run/secrets/openpost_mastodon_servers";
    #   value = config.sops.placeholder.openpost_mastodon_servers;
    # }
  ];

  openpostFileSecretEnvironment = lib.listToAttrs (
    map (secret: lib.nameValuePair secret.env secret.target) openpostFileSecrets
  );

  openpostFileSecretTemplates = lib.listToAttrs (
    map (
      secret:
      lib.nameValuePair "openpost-${secret.name}" {
        content = secret.value;
        mode = "0444";
      }
    ) openpostFileSecrets
  );

  openpostFileSecretMounts = map (
    secret:
    let
      templateName = "openpost-${secret.name}";
    in
    "--mount=type=bind,source=${config.sops.templates.${templateName}.path},target=${secret.target},ro"
  ) openpostFileSecrets;

in
{
  options.vps.openpost = {
    enable = lib.mkEnableOption "Enable OpenPost";

    edition = lib.mkOption {
      type = lib.types.enum [
        "selfhost"
        "cloud"
      ];
      default = "selfhost";
      description = ''
        OpenPost edition. `selfhost` keeps SQLite and local media defaults.
        `cloud` wires the container for Postgres and S3-compatible storage.
      '';
    };

    domain = lib.mkOption {
      type = lib.types.str;
      default = "app.openpost.social";
      description = "Domain for OpenPost";
    };

    timezone = lib.mkOption {
      type = lib.types.str;
      default = "Europe/Lisbon";
      description = "Timezone for OpenPost";
    };

    image = lib.mkOption {
      type = lib.types.str;
      default = "ghcr.io/rodrgds/openpost:latest";
      description = ''
        OpenPost container image. Pin this to a release tag or digest for
        reproducible production deploys.
      '';
    };

    pullPolicy = lib.mkOption {
      type = lib.types.enum [
        "always"
        "missing"
        "never"
      ];
      default = "always";
      description = ''
        Podman image pull policy for OpenPost. The default keeps the hosted
        service from reusing a stale local `latest` image after a Nix switch.
      '';
    };

    extraEnvironment = lib.mkOption {
      type = lib.types.attrsOf lib.types.str;
      default = { };
      description = ''
        Extra OpenPost environment variables. Use this for non-secret cloud
        settings, temporary overrides, or *_FILE pointers to mounted secrets.
      '';
    };

    extraEnvironmentFiles = lib.mkOption {
      type = lib.types.listOf lib.types.path;
      default = [ ];
      description = ''
        Extra env files passed to the OpenPost container. In cloud mode this
        should provide OPENPOST_DATABASE_URL, S3 credentials, Polar product
        secrets, or *_FILE pointers unless they are set via extraEnvironment.
      '';
    };

    extraOptions = lib.mkOption {
      type = lib.types.listOf lib.types.str;
      default = [ ];
      description = ''
        Extra Podman options for OpenPost. Use this to bind-mount additional
        secret files referenced by *_FILE environment variables.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    # Create persistent directories
    # Note: Container runs as user 'openpost' (UID 1000)
    systemd.tmpfiles.rules = [
      "d /var/lib/openpost 0755 root root -"
    ]
    ++ lib.optionals isCloud [
      "d /var/lib/openpost/postgres 0700 70 70 -"
      "d /var/backup/openpost 0750 root root -"
    ]
    ++ lib.optionals (!isCloud) [
      "d /var/lib/openpost/data 0755 1000 1000 -"
      "d /var/lib/openpost/data/db 0755 1000 1000 -"
      "d /var/lib/openpost/data/media 0755 1000 1000 -"
    ];

    # OpenPost application
    virtualisation.oci-containers.containers.openpost = {
      image = cfg.image;

      environment = {
        OPENPOST_PORT = toString openpostContainerPort;
        OPENPOST_EDITION = cfg.edition;
        OPENPOST_APP_URL = "https://${cfg.domain}";
        OPENPOST_PUBLIC_URL = "https://${cfg.domain}";
        OPENPOST_EXTRA_CORS_ORIGINS = "https://${cfg.domain}";
        OPENPOST_DISABLE_REGISTRATIONS = "false";
        LINKEDIN_DISABLE_THREAD_REPLIES = "true";
        X_REDIRECT_URI = "https://${cfg.domain}/api/v1/accounts/x/callback";
        LINKEDIN_REDIRECT_URI = "https://${cfg.domain}/api/v1/accounts/linkedin/callback";
        THREADS_REDIRECT_URI = "https://${cfg.domain}/api/v1/accounts/threads/callback";
        MASTODON_REDIRECT_URI = "https://${cfg.domain}/api/v1/accounts/mastodon/callback";
        TZ = cfg.timezone;
      }
      // openpostFileSecretEnvironment
      // (
        if isCloud then
          {
            OPENPOST_DATABASE_DRIVER = "postgres";
            OPENPOST_STORAGE_DRIVER = "s3";
          }
        else
          {
            OPENPOST_DATABASE_DRIVER = "sqlite";
            OPENPOST_DATABASE_PATH = "/data/db/openpost.db";
            OPENPOST_STORAGE_DRIVER = "local";
            OPENPOST_MEDIA_PATH = "/data/media";
            OPENPOST_MEDIA_URL = "https://${cfg.domain}/media";
          }
      )
      // cfg.extraEnvironment;

      environmentFiles =
        cfg.extraEnvironmentFiles
        ++ lib.optionals isCloud [
          config.sops.templates.openpost-cloud-env.path
        ];

      volumes = lib.optionals (!isCloud) [
        "/var/lib/openpost/data:/data"
      ];

      dependsOn = lib.optionals isCloud [ "openpost-postgres" ];

      ports = [
        "127.0.0.1:${toString openpostHostPort}:${toString openpostContainerPort}"
      ];

      extraOptions = [
        "--network=podman"
        "--pull=${cfg.pullPolicy}"
        "--health-cmd=wget --spider http://localhost:${toString openpostContainerPort}/api/v1/ready"
        "--health-interval=30s"
        "--health-timeout=3s"
        "--health-retries=3"
      ]
      ++ openpostFileSecretMounts
      ++ cfg.extraOptions;
    };

    virtualisation.oci-containers.containers.openpost-postgres = lib.mkIf isCloud {
      image = "docker.io/postgres:17-alpine";

      environmentFiles = [
        config.sops.templates.openpost-postgres-env.path
      ];

      volumes = [
        "/var/lib/openpost/postgres:/var/lib/postgresql/data"
      ];

      extraOptions = [
        "--network=podman"
        "--health-cmd=pg_isready -U ${openpostPostgresUser} -d ${openpostPostgresDatabase}"
        "--health-interval=10s"
        "--health-timeout=5s"
        "--health-retries=12"
      ];
    };

    sops.templates =
      openpostFileSecretTemplates
      // lib.optionalAttrs isCloud {
        "openpost-postgres-env" = {
          content = ''
            POSTGRES_USER=${openpostPostgresUser}
            POSTGRES_DB=${openpostPostgresDatabase}
            POSTGRES_PASSWORD=${config.sops.placeholder.openpost_postgres_password}
          '';
          mode = "0444";
        };
        "openpost-cloud-env" = {
          content = ''
            OPENPOST_DATABASE_URL=postgres://${openpostPostgresUser}:${config.sops.placeholder.openpost_postgres_password}@openpost-postgres:5432/${openpostPostgresDatabase}?sslmode=disable
            OPENPOST_S3_ENDPOINT=${config.sops.placeholder.openpost_s3_endpoint}
            OPENPOST_S3_REGION=${config.sops.placeholder.openpost_s3_region}
            OPENPOST_S3_BUCKET=${config.sops.placeholder.openpost_s3_bucket}
            OPENPOST_S3_ACCESS_KEY_ID=${config.sops.placeholder.openpost_s3_access_key_id}
            OPENPOST_S3_SECRET_ACCESS_KEY=${config.sops.placeholder.openpost_s3_secret_access_key}
            OPENPOST_S3_PUBLIC_BASE_URL=${config.sops.placeholder.openpost_s3_public_base_url}
            OPENPOST_LEGAL_ACCEPTANCE_REQUIRED=true
            OPENPOST_TERMS_URL=https://openpost.social/terms
            OPENPOST_PRIVACY_URL=https://openpost.social/privacy
            OPENPOST_TERMS_VERSION=2026-07-22
            OPENPOST_PRIVACY_VERSION=2026-07-22
            OPENPOST_SUPPORT_EMAIL=openpost@rgo.pt
            OPENPOST_SMTP_HOST=smtp.eu.mailgun.org
            OPENPOST_SMTP_PORT=465
            OPENPOST_SMTP_USERNAME=${config.sops.placeholder.ghost_mailgun_user}
            OPENPOST_SMTP_PASSWORD=${config.sops.placeholder.ghost_mailgun_password}
            OPENPOST_SMTP_FROM=openpost@rgo.pt
            OPENPOST_SMTP_TLS_MODE=tls
            OPENPOST_POLAR_ACCESS_TOKEN=${config.sops.placeholder.openpost_polar_access_token}
            OPENPOST_POLAR_WEBHOOK_SECRET=${config.sops.placeholder.openpost_polar_webhook_secret}
            OPENPOST_POLAR_CHECKOUT_SUCCESS_URL=https://${cfg.domain}/settings?tab=billing&checkout_id={CHECKOUT_ID}
            OPENPOST_POLAR_RETURN_URL=https://${cfg.domain}/settings?tab=billing
            OPENPOST_POLAR_STARTER_PRODUCT_ID=${config.sops.placeholder.openpost_polar_starter_product_id}
            OPENPOST_POLAR_CREATOR_PRODUCT_ID=${config.sops.placeholder.openpost_polar_creator_product_id}
            OPENPOST_POLAR_PRO_PRODUCT_ID=${config.sops.placeholder.openpost_polar_pro_product_id}
            OPENPOST_POLAR_TEAM_PRODUCT_ID=${config.sops.placeholder.openpost_polar_team_product_id}
            OPENPOST_POLAR_AGENCY_PRODUCT_ID=${config.sops.placeholder.openpost_polar_agency_product_id}
          '';
          mode = "0444";
        };
        "openpost-backup-env" = {
          content = ''
            RCLONE_CONFIG_OPENPOST_TYPE=s3
            RCLONE_CONFIG_OPENPOST_PROVIDER=Other
            RCLONE_CONFIG_OPENPOST_ENDPOINT=${config.sops.placeholder.openpost_s3_endpoint}
            RCLONE_CONFIG_OPENPOST_REGION=${config.sops.placeholder.openpost_s3_region}
            RCLONE_CONFIG_OPENPOST_ACCESS_KEY_ID=${config.sops.placeholder.openpost_s3_access_key_id}
            RCLONE_CONFIG_OPENPOST_SECRET_ACCESS_KEY=${config.sops.placeholder.openpost_s3_secret_access_key}
            OPENPOST_BACKUP_S3_BUCKET=${config.sops.placeholder.openpost_s3_bucket}
          '';
          mode = "0400";
        };
      };

    systemd.services.openpost-postgres-backup = lib.mkIf isCloud {
      description = "Backup OpenPost Postgres database";
      serviceConfig = {
        Type = "oneshot";
        ExecStart = pkgs.writeShellScript "openpost-postgres-backup" ''
          set -euo pipefail
          timestamp=$(${pkgs.coreutils}/bin/date +%Y%m%d_%H%M%S)
          backup_dir=/var/backup/openpost
          ${pkgs.coreutils}/bin/mkdir -p "$backup_dir"

          ${pkgs.podman}/bin/podman exec openpost-postgres pg_dump \
            -U ${openpostPostgresUser} \
            -d ${openpostPostgresDatabase} | ${pkgs.gzip}/bin/gzip > "$backup_dir/openpost_$timestamp.sql.gz"

          ${pkgs.findutils}/bin/find "$backup_dir" -name 'openpost_*.sql.gz' -mtime +14 -delete
        '';
      };
    };

    systemd.timers.openpost-postgres-backup = lib.mkIf isCloud {
      description = "Daily OpenPost Postgres backup";
      wantedBy = [ "timers.target" ];
      timerConfig = {
        OnCalendar = "daily";
        Persistent = true;
      };
    };

    systemd.services.openpost-media-backup = lib.mkIf isCloud {
      description = "Backup OpenPost S3 media with retained changed and deleted objects";
      serviceConfig = {
        Type = "oneshot";
        EnvironmentFile = config.sops.templates."openpost-backup-env".path;
        ExecStart = pkgs.writeShellScript "openpost-media-backup" ''
          set -euo pipefail
          timestamp=$(${pkgs.coreutils}/bin/date -u +%Y%m%d_%H%M%S)
          backup_root=/var/backup/openpost
          media_current="$backup_root/media-current"
          media_versions="$backup_root/media-versions/$timestamp"
          ${pkgs.coreutils}/bin/mkdir -p "$media_current" "$media_versions"

          ${pkgs.rclone}/bin/rclone sync \
            "openpost:$OPENPOST_BACKUP_S3_BUCKET" \
            "$media_current" \
            --backup-dir "$media_versions" \
            --fast-list \
            --checkers 8 \
            --transfers 4

          ${pkgs.rclone}/bin/rclone check \
            "openpost:$OPENPOST_BACKUP_S3_BUCKET" \
            "$media_current" \
            --one-way \
            --size-only

          ${pkgs.findutils}/bin/find "$backup_root/media-versions" \
            -mindepth 1 -maxdepth 1 -type d -mtime +14 \
            -exec ${pkgs.coreutils}/bin/rm -rf -- {} +
        '';
      };
    };

    systemd.timers.openpost-media-backup = lib.mkIf isCloud {
      description = "Daily OpenPost media backup";
      wantedBy = [ "timers.target" ];
      timerConfig = {
        OnCalendar = "daily";
        Persistent = true;
      };
    };

    systemd.services.openpost-restore-drill = lib.mkIf isCloud {
      description = "Restore and validate the latest OpenPost backup";
      after = [ "podman-openpost-postgres.service" ];
      requires = [ "podman-openpost-postgres.service" ];
      serviceConfig = {
        Type = "oneshot";
        ExecStart = pkgs.writeShellScript "openpost-restore-drill" ''
          set -euo pipefail
          backup_root=/var/backup/openpost
          latest_backup=$(${pkgs.findutils}/bin/find "$backup_root" -maxdepth 1 -type f -name 'openpost_*.sql.gz' -printf '%T@ %p\n' | ${pkgs.coreutils}/bin/sort -nr | ${pkgs.gawk}/bin/awk 'NR == 1 { print $2 }')
          if [ -z "$latest_backup" ]; then
            echo "No OpenPost database backup found" >&2
            exit 1
          fi

          ${pkgs.gzip}/bin/gzip -t "$latest_backup"
          restore_database="openpost_restore_drill_$(${pkgs.coreutils}/bin/date -u +%Y%m%d_%H%M%S)"
          database_created=false
          cleanup() {
            if [ "$database_created" = true ]; then
              ${pkgs.podman}/bin/podman exec openpost-postgres dropdb \
                --if-exists -U ${openpostPostgresUser} "$restore_database" >/dev/null
            fi
          }
          trap cleanup EXIT

          ${pkgs.podman}/bin/podman exec openpost-postgres createdb \
            -U ${openpostPostgresUser} "$restore_database"
          database_created=true
          ${pkgs.gzip}/bin/gzip -dc "$latest_backup" | ${pkgs.podman}/bin/podman exec -i \
            openpost-postgres psql -v ON_ERROR_STOP=1 \
            -U ${openpostPostgresUser} -d "$restore_database" >/dev/null

          table_count=$(${pkgs.podman}/bin/podman exec openpost-postgres psql -Atqc \
            "SELECT count(*) FROM information_schema.tables WHERE table_schema = 'public'" \
            -U ${openpostPostgresUser} -d "$restore_database")
          user_count=$(${pkgs.podman}/bin/podman exec openpost-postgres psql -Atqc \
            "SELECT count(*) FROM users" -U ${openpostPostgresUser} -d "$restore_database")
          workspace_count=$(${pkgs.podman}/bin/podman exec openpost-postgres psql -Atqc \
            "SELECT count(*) FROM workspaces" -U ${openpostPostgresUser} -d "$restore_database")
          post_count=$(${pkgs.podman}/bin/podman exec openpost-postgres psql -Atqc \
            "SELECT count(*) FROM posts" -U ${openpostPostgresUser} -d "$restore_database")
          database_media_count=$(${pkgs.podman}/bin/podman exec openpost-postgres psql -Atqc \
            "SELECT count(*) FROM media_attachments" -U ${openpostPostgresUser} -d "$restore_database")
          media_file_count=$(${pkgs.findutils}/bin/find "$backup_root/media-current" -type f | ${pkgs.coreutils}/bin/wc -l)

          if [ "$table_count" -lt 10 ]; then
            echo "Restore has too few public tables: $table_count" >&2
            exit 1
          fi
          if [ "$database_media_count" -gt 0 ] && [ "$media_file_count" -eq 0 ]; then
            echo "Restore contains media records but the media snapshot is empty" >&2
            exit 1
          fi

          checked_at=$(${pkgs.coreutils}/bin/date -u +%Y-%m-%dT%H:%M:%SZ)
          backup_name=$(${pkgs.coreutils}/bin/basename "$latest_backup")
          backup_size=$(${pkgs.coreutils}/bin/wc -c < "$latest_backup")
          evidence_tmp=$(${pkgs.coreutils}/bin/mktemp "$backup_root/restore-drill-latest.json.XXXXXX")
          {
            printf '{\n'
            printf '  "status": "passed",\n'
            printf '  "checked_at": "%s",\n' "$checked_at"
            printf '  "backup": "%s",\n' "$backup_name"
            printf '  "backup_bytes": %s,\n' "$backup_size"
            printf '  "public_tables": %s,\n' "$table_count"
            printf '  "users": %s,\n' "$user_count"
            printf '  "workspaces": %s,\n' "$workspace_count"
            printf '  "posts": %s,\n' "$post_count"
            printf '  "database_media": %s,\n' "$database_media_count"
            printf '  "media_files": %s\n' "$media_file_count"
            printf '}\n'
          } > "$evidence_tmp"
          ${pkgs.coreutils}/bin/chmod 0640 "$evidence_tmp"
          ${pkgs.coreutils}/bin/mv "$evidence_tmp" "$backup_root/restore-drill-latest.json"
        '';
      };
    };

    systemd.timers.openpost-restore-drill = lib.mkIf isCloud {
      description = "Weekly OpenPost restore drill";
      wantedBy = [ "timers.target" ];
      timerConfig = {
        OnCalendar = "Sun 04:00";
        Persistent = true;
      };
    };

    vps.caddy.internalPorts.openpost = openpostHostPort;
  };
}
